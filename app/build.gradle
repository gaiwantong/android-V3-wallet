apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    // Introduced to prevent crash during DEXing step since SDK 23, but also speeds up DEXing in general
    dexOptions {
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        applicationId "piuk.blockchain.android"
        targetSdkVersion 24
        versionCode 222
        versionName "6.1.31"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_key", "AIzaSyB_5WN2exwvF4pqQ6rW6JdkHcYWjZOim4w"
            multiDexEnabled false
            buildConfigField "boolean", "DOGFOOD", "false"
        }
        debug {
            resValue "string", "google_maps_key", "AIzaSyASTnx7wjsCTkMr_RtLQYTzMAf7A9SWIMw"
            multiDexEnabled true
            buildConfigField "boolean", "DOGFOOD", "false"
        }
        dogfood {
            signingConfig signingConfigs.debug
            versionNameSuffix '-DOGFOOD'
            applicationIdSuffix ".dogfood"
            debuggable true
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_key", "AIzaSyB_5WN2exwvF4pqQ6rW6JdkHcYWjZOim4w"
            multiDexEnabled false
            buildConfigField "boolean", "DOGFOOD", "true"
        }
    }

    productFlavors {
        prod {
            // For release and testing on <API21 devices
            minSdkVersion 14
        }
        dev {
            // For everyday work, setting minSdkVersion to 21 bypasses MultiDex, resulting in
            // much faster builds
            minSdkVersion 21
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        fatal 'StopShip'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        enabled = true
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
        // This allows "unstable" builds on CI rather than straight failures
        ignoreFailures true
    }
}

ext {
    supportVersion = '24.2.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"

    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.bitcoinj:bitcoinj-core:0.13.4'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-codec:commons-codec:1.10'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    compile 'com.neovisionaries:nv-websocket-client:1.19'
    compile('com.google.zxing:core:3.2.1') { transitive = true }
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.mukesh:countrypicker:1.1.3'
    // RxJava
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'io.reactivex:rxandroid:1.2.1'
    // Dagger2
    compile 'com.google.dagger:dagger:2.6'
    apt 'com.google.guava:guava:19.0'
    apt 'com.google.dagger:dagger-compiler:2.6'

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.3.1'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile ('org.robolectric:robolectric:3.1') {
        exclude group: 'com.google.guava'
        exclude group: 'com.google.protobuf'
    }

    //Classes from the android framework won't work in local jvm unit tests. Added below for unit test purposes only
    testCompile 'org.json:json:20140107'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
    }
}

// Required for RetroLambda
repositories {
    jcenter()
}